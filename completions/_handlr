#compdef dutils

_dutils_types() {
  declare -a types
  types=(${${(f)"$(dutils autocomplete -m)"}})
  _describe -t types "types" types
}

_dutils_desktops() {
  declare -a desktops
  desktops=(${${(f)"$(dutils autocomplete -d | tr '\t' ':')"}})
  _describe -t desktops "desktops" desktops
}

_dutils_commands() {
  declare -a subcommands
  subcommands=(
      'list:List default apps and the associated handlers'
      'open:Open a path/URL with its default handler'
      'set:Set the default handler for mime/extension'
      'unset:Unset the default handler for mime/extension'
      'launch:Launch the handler for specified extension/mime with optional arguments'
      'get:Get handler for this mime/extension'
      'add:Add a handler for given mime/extension Note that the first handler is the default'
  )
  _describe -t dutils-commands "command" subcommands
}

_dutils_subcommand () {
  case "$words[1]" in
    (list)
      ;;
    (open)
      _alternative '1:filename/path:_files'
      ;;
    (get|unset)
      _arguments ':types:_dutils_types'
      ;;
    (launch)
      _arguments \
          '1:types:_dutils_types' \
          '2:filename/path:_files'
      ;;
    (set|add)
      _arguments \
          '1:type:_dutils_types' \
          '2:desktop:_dutils_desktops'
      ;;
    (*)
      _message 'Unknown subcommand'
  esac
}

_dutils () {
  local curcontext="$curcontext" state line
  typeset -A opt_args

  _arguments -C \
    '--version[get version]:' \
    '--help[get help]:' \
    '(-): :->command' \
    '(-)*:: :->arguments'

  case $state in
    (command)
      _dutils_commands
      ;;
    (arguments)
      curcontext=${curcontext%:*:*}:dutils-$words[1]:
      _dutils_subcommand
      ;;
  esac
}

_dutils "$@"
